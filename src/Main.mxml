<?xml version="1.0" encoding="utf-8"?>
<local:MainBase xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx"
				xmlns:aoi="http://www.aoi.moe/"
				xmlns:local="*"
				xmlns:components="components.*"
				xmlns:models="models.*"
				
				title="0133训练机"
				width="560"
				height="360"
				skinClass="skins.MainSkin"
				showStatusBar="false"
				applicationComplete="applicationCompleteHandler(event)">
	
	<fx:Style source="css/Main.css"/>
	
	<fx:Script>
		<![CDATA[
			import components.ProgressTitleWindow;
			import errors.BMSError;
			import errors.BMSError;
			import flash.display.Loader;
			import flash.events.Event;
			import flash.events.UncaughtErrorEvent;
			import flash.filesystem.File;
			import flash.net.registerClassAlias;
			import flash.net.URLLoader;
			import flash.net.URLLoaderDataFormat;
			import flash.net.URLRequest;
			import flash.system.Worker;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			import mx.validators.Validator;
			import workers.BackgroundWorker;
			
			public static var current:Main;
			
			//[Embed(source="/../libs/workers/BackgroundWorker.swf", mimeType="application/octet-stream")]
			//private static const WORKER:Class;
			
			private const file:File = new File();
			
			private var progressTitleWindow:ProgressTitleWindow;
			
			//☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆
			//  Worker
			//☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆

			private var bWorker:Worker;
			
			private var m2bChannel:MessageChannel;
			private var b2mChannel:MessageChannel;
			
			private const workerLoader:URLLoader = new URLLoader();
			
			private const workerRequest:URLRequest = new URLRequest(File.applicationDirectory.resolvePath('workers/BackgroundWorker.swf').url);
			
			registerClassAlias('flash.filesystem.File', File);
			/**
			 * @see BackgroundWorker#applicationDirectory
			 */
			private function createBackgroundWorker(event:Event):void
			{
				trace('createBGWroker~');
				bWorker = WorkerDomain.current.createWorker(workerLoader.data, true);
				
				m2bChannel = Worker.current.createMessageChannel(bWorker);
				b2mChannel = bWorker.createMessageChannel(Worker.current);
				b2mChannel.addEventListener(Event.CHANNEL_MESSAGE, onB2MChannelMessage);
				
				bWorker.setSharedProperty('m2bChannel', m2bChannel);
				bWorker.setSharedProperty('b2mChannel', b2mChannel);
				bWorker.setSharedProperty('applicationDirectory', new File(File.applicationDirectory.nativePath));
				
				bWorker.start();
			}
			
			private function removeProgressTitleWindow(event:CloseEvent = null):void
			{
				PopUpManager.removePopUp(progressTitleWindow);
			}
			
			/**
			 * 修复 NumericStepper 在 value 为整数时以小数显示的问题。
			 * 但是它虽然显示小数，也不会影响到 OSUPrinter，它打印出的还是整数。
			 */
			protected function stepper_formatFunc(value:Number):String
			{
				const intager:uint = uint(value);
				return value == intager ? intager.toString() : value.toString();
			}
			
			// TODO: 把选择编码UI补上。还有去BMS2OSUConverter里把+1K的判断搞下，再来这补UI。
			
			//☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆
			//  Controllers
			//☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆
			
			//☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆
			//  Application
			//☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆
			
			protected function applicationCompleteHandler(event:FlexEvent):void
			{
				//loaderInfo.uncaughtErrorEvents.addEventListener(UncaughtErrorEvent.UNCAUGHT_ERROR, onUncaughtError);
				
				current = this;
				file.addEventListener(Event.CANCEL, file_cancelHandler);
				workerLoader.dataFormat = URLLoaderDataFormat.BINARY;
				workerLoader.addEventListener(Event.COMPLETE, createBackgroundWorker);
			}
			
			//☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆
			//  Worker
			//☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆
			
			private function onB2MChannelMessage(event:Event):void
			{
				if (!b2mChannel.messageAvailable) return;
				
				const msg:String = b2mChannel.receive(true);
				
				//trace( "msg : " + msg );
				
				if (msg in this)
				{
					this[msg]();
				}
				else switch (msg)
				{
					case BackgroundWorker.HEAD_BMS_PROGRESS:
						progressTitleWindow.setBMSProgress.apply(null, b2mChannel.receive(true));
						break;
					
					case BackgroundWorker.HEAD_BMSPACK_PROGRESS:
						progressTitleWindow.setBMSPackProgress.apply(null, b2mChannel.receive(true));
						break;
						
					default:
						throw new Error('未知的消息：' + msg);
				}
			}
			
			//☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆
			//  Meassage
			//☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆
			
			public function ready():void 
			{
				currentState = 'converting';
				
				progressTitleWindow = new ProgressTitleWindow();
				
				PopUpManager.addPopUp(progressTitleWindow, this, true);
				PopUpManager.centerPopUp(progressTitleWindow);

				m2bChannel.send(BackgroundWorker.HEAD_START);
				m2bChannel.send(config);
			}
			
			public function collecting():void
			{
				progressTitleWindow.currentState = 'collecting_bmsPacks';
			}
			
			public function arranging():void
			{
				progressTitleWindow.currentState = 'arranging';
			}
			
			/**
			 * 这儿的函数全用 public，以便 this[msg](); 能方便调用。
			 */
			// TODO: Custom namespace?
			public function not_found():void
			{
				Alert.show('然而你选的 BMS 文件夹中并没有找到 BMS', 'あれ？', Alert.OK, null, removeProgressTitleWindow);
			}
			
			public function critical_error():void 
			{
				const error:Object = b2mChannel.receive(true);
				Alert.show(error.message, '不幸的事故', Alert.OK, null, removeProgressTitleWindow);
				bWorker.terminate();
			}
			
			registerClassAlias('Error', Error);
			registerClassAlias('errors.BMSError', BMSError);
			public function error():void 
			{
				const error:Object = b2mChannel.receive(true);
				progressTitleWindow.addLog('警告：' + error.message + '\n')
				trace(error.stackTrace);
			}
			
			registerClassAlias("Event", Event);
			public function error_event():void 
			{
				const event:Event = b2mChannel.receive(true) as Event;
				trace(event);
				Alert.show(event.toString(), '不幸的事故', Alert.OK, null, removeProgressTitleWindow);
				bWorker.terminate();
			}
			
			public function cancel():void
			{
				bWorker.terminate();
				removeProgressTitleWindow();
			}
			
			public function complete():void
			{
				Alert.show('BMS 已经全部转换完毕！', 'やっっったぁぁぁぁぁ！', Alert.OK, null, removeProgressTitleWindow);
			}
			
			//☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆
			//  Directory
			//☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆
			
			protected function selectBMSBtn_clickHandler(event:MouseEvent):void
			{
				file.addEventListener(Event.SELECT, bmsDir_selectHandler);
				file.browseForDirectory('选择 BMS 文件夹');
			}
			
			protected function selectOutputBtn_clickHandler(event:MouseEvent):void
			{
				file.addEventListener(Event.SELECT, outputDir_selectHandler);
				file.browseForDirectory('选择输出文件夹');
			}
			
			protected function bmsDir_selectHandler(event:Event):void
			{
				file.removeEventListener(Event.SELECT, arguments.callee);
				
				config.bmsDirStr = decodeURIComponent(file.url);
			}
			
			protected function outputDir_selectHandler(event:Event):void
			{
				file.removeEventListener(Event.SELECT, arguments.callee);
				
				config.outputDirStr = decodeURIComponent(file.url);
			}
			
			private function file_cancelHandler(e:Event):void 
			{
				file.removeEventListener(Event.SELECT, bmsDir_selectHandler);
				file.removeEventListener(Event.SELECT, outputDir_selectHandler);
			}
			
			registerClassAlias('models.Config', Config);
			protected function convertBtn_clickHandler(event:MouseEvent):void
			{
				const res:Array = Validator.validateAll(validators);
				if (res.length != 0) return;
				
				try 
				{
					const bmsDirStr:File = new File(config.bmsDirStr);
					if (!bmsDirStr.exists)
					{
						Alert.show('BMS 文件夹不存在啊', 'あれ？', Alert.OK, null, removeProgressTitleWindow);
						return;
					}
				}
				catch (error:ArgumentError)
				{
					Alert.show('别乱输 BMS 文件夹路径啊', 'あれ？', Alert.OK, null, removeProgressTitleWindow);
					return;
				}
				
				try 
				{
					const outputDirStr:File = new File(config.outputDirStr);
					if (!bmsDirStr.exists)
					{
						Alert.show('输出文件夹不存在啊', 'あれ？', Alert.OK, null, removeProgressTitleWindow);
						return;
					}
				}
				catch (error:ArgumentError)
				{
					Alert.show('别乱输输出文件夹路径啊', 'あれ？', Alert.OK, null, removeProgressTitleWindow);
					return;
				}
				
				workerLoader.load(workerRequest);
				//createBackgroundWorker();
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<models:Config id="config" bmsDirStr="file:///D:/test_bms/[Silent Spica (a_hisa)] Anhedonia" outputDirStr="file:///D:/佳宸/Desktop" />
		
		<s:ArrayList id="encodings">
			<fx:String>shift_jis</fx:String>
			<fx:String>gb18030</fx:String>
			<fx:String>utf8</fx:String>
		</s:ArrayList>
		
		<s:ArrayList id="scratchTypes">
			<fx:String>是</fx:String>
			<fx:String>否</fx:String>
		</s:ArrayList>
		
		<s:ArrayList id="hitSoundTypes">
			<fx:String>有</fx:String>
			<fx:String>无</fx:String>
		</s:ArrayList>
		
		<fx:Array id="validators">
			<mx:StringValidator source="{bmsDirInput}" property="text" required="true"/>
			<mx:StringValidator source="{outputDirInput}" property="text" required="true"/>
		</fx:Array>
		
	</fx:Declarations>
	
	<local:states>
		<s:State name="default"/>
		<s:State name="converting"/>
	</local:states>
	
	<s:Form id="form" horizontalCenter="0" verticalCenter="0" defaultButton="{submitButton}">
		<s:FormItem label="BMS 文件夹：" width="100%" required="true">
			<s:layout>
				<s:HorizontalLayout/>
			</s:layout>
			<s:TextInput id="bmsDirInput" text="@{config.bmsDirStr}"/>
			<s:Button label="选择"
					  click="selectBMSBtn_clickHandler(event)"/>
		</s:FormItem>
		
		<s:FormItem label="输出文件夹：" width="100%" required="true">
			<s:layout>
				<s:HorizontalLayout/>
			</s:layout>
			<s:TextInput id="outputDirInput" text="@{config.outputDirStr}"/>
			<s:Button label="选择"
					  click="selectOutputBtn_clickHandler(event)"/>
		</s:FormItem>
		
		<s:FormItem label="编码：" width="100%">
			<s:ComboBox selectedItem="@{config.encoding}"
						dataProvider="{encodings}"/>
		</s:FormItem>
		
		<s:FormItem label="OD：" width="100%">
			<s:layout>
				<s:HorizontalLayout verticalAlign="middle"/>
			</s:layout>
			<s:HSlider value="@{config.OD}" snapInterval="0.1" dataTipPrecision="1"/>
			<s:NumericStepper value="@{config.OD}" snapInterval="0.1" stepSize="0.1" valueFormatFunction="stepper_formatFunc"/>
		</s:FormItem>
		
		<s:FormItem label="HP：" width="100%">
			<s:layout>
				<s:HorizontalLayout verticalAlign="middle"/>
			</s:layout>
			<s:HSlider value="@{config.HP}" snapInterval="0.1" dataTipPrecision="1"/>
			<s:NumericStepper value="@{config.HP}" snapInterval="0.1" stepSize="0.1" valueFormatFunction="stepper_formatFunc"/>
		</s:FormItem>
		
		<s:FormItem label="是否附加7k无皿谱：" width="100%">
			<s:DropDownList selectedItem="@{config.addNoScratch}"
							dataProvider="{scratchTypes}"/>
		</s:FormItem>
		
		<s:FormItem label="默认击打音效：" width="100%">
			<s:helpContent>
				<s:Label text="无 Key 音谱会附加击打音效"/>
			</s:helpContent>
			<s:DropDownList selectedItem="@{config.hitSoundType}"
							dataProvider="{hitSoundTypes}"/>
		</s:FormItem>
		
		<s:FormItem label="默认击打音效音量：" width="100%">
			<s:NumericStepper minimum="0" maximum="100" value="@{config.volume}"/>
		</s:FormItem>
		
		<s:FormItem>
			<s:Button id="submitButton" label="训练" click="convertBtn_clickHandler(event)"/>
		</s:FormItem>
	</s:Form>
	
</local:MainBase>

